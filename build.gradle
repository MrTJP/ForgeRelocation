buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'scala'

version = "0.0.2"
group = "mrtjp.forgerelocation"
archivesBaseName = "forgerelocation"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
compileScala {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.0.2501"
    runDir = "run"
    mappings = "snapshot_20170624"
}

repositories {
    jcenter()
    maven { url "https://modmaven.k-4u.nl/" }
}

dependencies {
    compile projectOrMvn(":MrTJPCore", "mrtjp:MrTJPCore:1.12.2-+:deobf")
    compile projectOrMvn(":CodeChickenLib", "codechicken:CodeChickenLib:1.12-+:deobf")

    runtime "mezz.jei:jei_${project.minecraft.version}:+"

    compile "org.scala-lang:scala-library:2.11.+"
    compile "org.scala-lang:scala-compiler:2.11.+"
}

def projectOrMvn(String project, String artifact) { findProject(project) ?: artifact }

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
